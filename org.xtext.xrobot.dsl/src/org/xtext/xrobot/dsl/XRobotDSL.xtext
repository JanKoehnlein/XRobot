/*******************************************************************************
 * Copyright (c) 2014 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
grammar org.xtext.xrobot.dsl.XRobotDSL with org.eclipse.xtext.xbase.Xbase

generate xRobotDSL "http://www.xtext.org/xrobot/dsl/XRobotDSL"

Program:
	imports=XImportSection?
	'robot' (name=ID | name = STRING)
	'author' (author=ID | author=STRING)
	(functions+=Function | variables+=Variable | modes+=Mode)*;

Mode:
	name=ID ('on' condition=XExpression)?
	action=XBlockExpression
	('when' 'left' whenLeft=XBlockExpression)?;

Function:
	'def' returnType=JvmTypeReference? name=ValidID 
	'(' (parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ')' 
	body=XBlockExpression;

Variable:
	'var' type=JvmTypeReference? name=ValidID ('=' initializer=XExpression)?;

ValidID:
	ID | 'robot' | 'author' | 'on' | 'when' | 'left';